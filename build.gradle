import org.gradle.plugins.signing.Sign

plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.3.0'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
}

// Semantic versioning with axion-release
scmVersion {
    tag {
        prefix = 'v'
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

def pipestreamBomVersion = findProperty('pipestreamBomVersion') ?: '0.7.1-new_proto_interface-SNAPSHOT'

// 1. Define the export path
def bufExportDir = layout.buildDirectory.dir("generated/buf-protos")

// 2. The Sync Tasks
tasks.register('syncAccountProtos', Exec) {
    group = 'protobuf'
    description = 'Exports account proto files from Buf Registry'
    commandLine 'buf', 'export', 'buf.build/pipestreamai/admin',
            '--output', bufExportDir.get().asFile.path
    outputs.dir(bufExportDir)
    doFirst {
        bufExportDir.get().asFile.mkdirs()
    }
}

tasks.register('syncProtos') {
    group = 'protobuf'
    description = 'Syncs all proto definitions from Buf Registry'
    dependsOn 'syncAccountProtos'
}

// 3. Configure Quarkus to find the protos
quarkus {
    // Use 'proto-directory', not 'proto-source-path'
    quarkusBuildProperties.put("quarkus.grpc.codegen.proto-directory", bufExportDir.get().asFile.path)
    // Enable gRPC code generation scanning
    quarkusBuildProperties.put("quarkus.grpc.codegen.enabled", "true")
}

// 4. CRITICAL: Fix Build Order & IDE Visibility
sourceSets {
    main {
        java {
            // Tell IntelliJ/Gradle where Quarkus puts the generated Java files
            srcDir layout.buildDirectory.dir("classes/java/quarkus-generated-sources/grpc")
        }
    }
}

// 5. Hook into the correct Quarkus tasks
// We must run syncProtos BEFORE Quarkus tries to generate code
tasks.named('quarkusGenerateCode').configure {
    dependsOn 'syncProtos'
}
tasks.named('quarkusGenerateCodeDev').configure {
    dependsOn 'syncProtos'
}
tasks.named('quarkusGenerateCodeTests').configure {
    dependsOn 'syncProtos'
}

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    implementation platform("ai.pipestream:pipestream-bom:${pipestreamBomVersion}")

    // Core Quarkus dependencies
    implementation libs.quarkus.arc
    implementation libs.quarkus.rest
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-jdbc-mysql'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation libs.quarkus.grpc
    implementation 'io.quarkus:quarkus-smallrye-stork'

    // Kafka dependencies
    implementation 'io.quarkus:quarkus-messaging-kafka'
    // Updated Apicurio dependency
    implementation libs.quarkus.apicurio.registry.protobuf

    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'
    implementation 'io.smallrye.stork:stork-service-registration-consul'

    // Pipestream platform libraries (managed via BOM/catalog)
    implementation libs.pipestream.devservices
    implementation 'ai.pipestream:quarkus-dynamic-grpc'
    implementation 'ai.pipestream:pipestream-service-registration:0.7.1-new_proto_interface-SNAPSHOT'

    // Test dependencies
    testImplementation libs.quarkus.junit5
    testImplementation libs.rest.assured
    // Pending discussion on Wiremock server
    // testImplementation libs.wiremock.grpc.extension.standalone
    testImplementation libs.awaitility
    testImplementation libs.apicurio.registry.protobuf.serde
    testImplementation platform('org.testcontainers:testcontainers-bom:2.0.1')
    testImplementation 'org.testcontainers:testcontainers'
}


group = 'ai.pipestream'
version = "${scmVersion.version}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    environment "TESTCONTAINERS_RYUK_DISABLED", "false"

    // Add JVM option for JDK 24+
    def javaVersion = JavaVersion.current().majorVersion.toInteger()
    if (javaVersion >= 24) {
        jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Fix sourcesJar dependency on generated code
afterEvaluate {
    tasks.named('sourcesJar', Jar) {
        dependsOn 'quarkusGenerateCode'
    }
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) { publication ->
            from components.java
            pom {
                name.set('Account Service')
                description.set('Account management service for multi-tenant support in the Pipestream platform')
                url.set('https://github.com/ai-pipestream/account-service')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/license/mit')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Engine Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/account-service.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/account-service.git')
                    url.set('https://github.com/ai-pipestream/account-service')
                }
            }
        }
    }
    repositories {
        mavenLocal()

        // GitHub Packages
        def ghRepo = System.getenv('GITHUB_REPOSITORY') ?: 'ai-pipestream/account-service'
        def ghActor = System.getenv('GITHUB_ACTOR') ?: (project.findProperty('gpr.user') ?: System.getenv('GPR_USER'))
        def ghToken = System.getenv('GITHUB_TOKEN') ?: (project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN'))
        if (ghActor && ghToken) {
            maven {
                name = 'GitHubPackages'
                url = uri("https://maven.pkg.github.com/${ghRepo}")
                credentials {
                    username = ghActor
                    password = ghToken
                }
            }
        }
    }
}

// GPG Signing (uses org-level secrets)
signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Ensure signing tasks run before NMCP publication
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}

// Maven Central publishing via NMCP
nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
