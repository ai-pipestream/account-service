plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://git.rokkon.com/api/packages/io-pipeline/maven"
        credentials {
            username = project.findProperty("gitea.user") ?: System.getenv("GITEA_USER")
            password = project.findProperty("gitea.token") ?: System.getenv("GITEA_TOKEN")
        }
    }
}

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'

    // Dev services infrastructure (shared Docker Compose stack)
    runtimeOnly 'io.pipeline:devservices-docker-compose:1.0.0-SNAPSHOT'

    // Use published BOM from Maven Local or Gitea
    implementation platform('io.pipeline:pipeline-bom:1.0.0-SNAPSHOT')

    // Core Quarkus dependencies
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-jdbc-mysql'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.quarkus:quarkus-smallrye-stork'

    // Kafka dependencies
    implementation 'io.quarkus:quarkus-messaging-kafka'
    implementation libs.apicurio.registry.protobuf.serde

    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'
    implementation 'io.smallrye.stork:stork-service-registration-consul'

    // Pipeline libraries (published to Maven Local or Gitea)
    implementation 'io.pipeline:grpc-stubs:1.0.0-SNAPSHOT'
    implementation 'io.pipeline:dynamic-grpc-registration-clients:1.0.0-SNAPSHOT'
    implementation 'io.pipeline:dynamic-grpc:1.0.0-SNAPSHOT'

    // Test dependencies
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.pipeline:grpc-wiremock:1.0.0-SNAPSHOT'
    testImplementation libs.smallrye.reactive.messaging.in.memory
    integrationTestImplementation 'io.grpc:grpc-netty-shaded'
    integrationTestImplementation 'io.grpc:grpc-protobuf'
    integrationTestImplementation 'io.grpc:grpc-stub'
}

group = 'io.pipeline'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(tasks.named('quarkusBuild')) {
                classifier = 'runner'
            }
            pom {
                name = 'Account Service'
                description = 'User account management and authentication service'
                url = 'https://github.com/io-pipeline/account-service'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        name = 'Pipeline Engine Team'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/io-pipeline/account-service.git'
                    developerConnection = 'scm:git:ssh://github.com/io-pipeline/account-service.git'
                    url = 'https://github.com/io-pipeline/account-service'
                }
            }
        }
    }

    repositories {
        // Publish to Gitea Maven registry (primary)
        maven {
            name = "Gitea"
            url = "https://git.rokkon.com/api/packages/io-pipeline/maven"
            credentials {
                username = project.findProperty("gitea.user") ?: System.getenv("GITEA_USER")
                password = project.findProperty("gitea.token") ?: System.getenv("GITEA_TOKEN")
            }
        }

        // Publish to GitHub Packages (mirror)
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/io-pipeline/account-service"
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: System.getenv("GH_USER")
                password = System.getenv("GITHUB_TOKEN") ?: System.getenv("GH_PAT")
            }
        }
    }
}